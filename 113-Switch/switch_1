# Go+ by Example: Switch
// Go+ provides multi branch conditional statement switch,
// to perform different actions under different conditions.

// Switch statements express conditionals across many branches.

## The first type: a switch statement with expression

// Here's a basic switch.

// Example 1: a basic switch
season := 3
switch season {
case 1:
	println "Spring"
case 2:
	println "Summer"
case 3:
	println "Autumn"
case 4:
	println "Winter"
}

## The second type: a switch with default case

// You can use the optional default case in this example as well.
// When no other case matches, the default case is executed.

// Example 2: a switch example with default case
dayOfTheWeek := 2
switch dayOfTheWeek {
case 0:
	println "Today is Sunday."
case 6:
	println "Today is Saturday."
default:
	println "Today is a weekday."
}

## The third type: a switch with multiple expressions in one case
// You can use commas to separate multiple expressions in the same case statement.
// So long as one of the expressions is matched, the case will be executed.

// Example 3: a switch example of multiple expressions in case
var letter string = "u"
switch letter {
case "a", "e", "i", "o", "u":
	printf "%s is a vowel.\n", letter
default:
	printf "%s isn't a vowel.\n", letter
}

## The fourth type: a switch without an expression

// switch without an expression is an alternate way to express if/else logic.
// Here we also show how the case expressions can be non-constants.

// Example 4: a switch without expression
var num int = 80
switch {
case num < 50:
	printf "%d < 50\n", num
case num < 100:
	printf "%d < 100\n", num
case num < 200:
	printf "%d < 200\n", num
}

## The fifth type: a switch without an expression

// The switch in go+ defaults to a break at the end of each case.
// Use fallthrough to enforce the code for the subsequent cases.

// Example 5: switch example with fallthrough
var score int = 80
switch {
case score < 50:
	printf "%d < 50\n", score
	fallthrough
case score < 100:
	printf "%d < 100\n", score
	fallthrough
case score < 200:
	printf "%d < 200\n", score
}
